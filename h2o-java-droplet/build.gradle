/*
 * This is a simple build file for building H2O applications.
 * 
 * For more details please follow README.md
 */

// Apply the java plugin to add support for Java
apply plugin: 'java-library'
// Support local launch of application
apply plugin: 'application'
mainClassName = 'water.droplets.H2OJavaDroplet'
//
// Configure project properties
//
ext { 
  // Configure h2o-dev dependency information
  h2oBranch = 'rel-zahradnik'
  h2oBuildNumber = '7'
  h2oProjectVersion = "3.30.0.${h2oBuildNumber}"
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'maven central' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()

    // Cloudera dependencies
    maven {
      url "https://repository.cloudera.com/artifactory/cloudera-repos/"
    }

    // Hortonworks dependencies
    maven {
      url "https://repo.hortonworks.com/content/repositories/releases/"
    }

    // Include build h2o-dev dependencies if branch if pulling from master
    if (h2oBranch == 'master') {
      maven {
        url "https://s3.amazonaws.com/h2o-release/h2o/${h2oBranch}/${h2oBuildNumber}/maven/repo/"
      }
    }

    // Enable 'maven local' for resolving your locally cached dependencies
    // Useful for cross development for example with h2o-dev
    if (h2oBuildNumber == '99999') mavenLocal()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Define dependency on core of H2O
    api "ai.h2o:h2o-core:${h2oProjectVersion}"
    // Define dependency on H2O algorithm 
    api "ai.h2o:h2o-algos:${h2oProjectVersion}"
    // Demands web support 
    api "ai.h2o:h2o-web:${h2oProjectVersion}"
    // Contains H2O entry points
    api "ai.h2o:h2o-app:${h2oProjectVersion}"

    // H2O uses jUnit for testing 
    testImplementation 'junit:junit:4.11'
}

//
// Project specific settings
//

// Setup group ID for this project
group = "ai.h2o"

//
// Setup Java plugin
//
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava { 
    options.debug = true 
}
