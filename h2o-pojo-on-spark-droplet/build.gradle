buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'scala'
apply plugin: 'com.github.johnrengelman.shadow'
// Support local launch of application
apply plugin: 'application'
mainClassName = 'water.droplets.UsePojoDroplet'

ext {
  scalaBaseVersion = "2.12"
  scalaVersion = "2.12.10"
  sparkVersion = "3.0.0"
}

repositories {
   mavenCentral()
}

dependencies {
    api "ai.h2o:h2o-genmodel:3.30.0.7"
    // Spark dependencies
    // - core
    api "org.apache.spark:spark-core_${scalaBaseVersion}:${sparkVersion}"
    // - SQL component
    api "org.apache.spark:spark-sql_${scalaBaseVersion}:${sparkVersion}"
    // Add joda optional convert library which is required in Scala environment
    api "org.joda:joda-convert:1.7"
    // And Scala library
    api "org.scala-lang:scala-library:${scalaVersion}"
}

// Setup resources
sourceSets {
    main {
        java {
            srcDirs += "src/main/pojo"
        }
    }
}

configurations {
    shadowApi {
        extendsFrom api
    }
}

shadowJar {
    // Configure name of output jar as sparkling-water-droplet-app.jar
    archiveAppendix = 'app'
    configurations = [project.configurations.shadowApi]
    mergeServiceFiles()
    archiveBaseName = "${archiveBaseName}_${scalaBaseVersion}"
    zip64 = true
}

artifacts {
    api shadowJar
}

